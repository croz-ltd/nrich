plugins {
  id "fr.brouillard.oss.gradle.jgitver" version "$gradleJgitverPluginVersion"
  id "io.freefair.aggregate-javadoc-legacy" version "$gradleAggregateJavadocPluginVersion"
  id "io.github.gradle-nexus.publish-plugin" version "$gradlePublishPluginVersion"
  id "jacoco-report-aggregation"
}

reporting {
  reports {
    testCodeCoverageReport(JacocoCoverageReport) {
      testType = TestSuiteType.UNIT_TEST
    }
  }
}

jgitver {
  mavenLike true
  policy {
    pattern = "(.*)"
    transformations = ["IGNORE"]
  }
}

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }

  configurations {
    compileOnly.extendsFrom(annotationProcessor)

    springBom
    annotationProcessor.extendsFrom(springBom)
    implementation.extendsFrom(springBom)
    testAnnotationProcessor.extendsFrom(springBom)
  }
}

Project parent = project

subprojects { Project subproject ->
  boolean isPlatformProject = subproject.name.endsWith("bom")

  apply plugin: "maven-publish"
  apply plugin: "signing"

  if (isPlatformProject) {
    apply plugin: "java-platform"
  }
  else {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "jacoco"

    sourceCompatibility = "17"

    java {
      ["envers", "reactor", "web", "webMvc", "webFlux", "hibernate", "spel"].forEach {
        registerFeature(it) {
          usingSourceSet(sourceSets.main)
        }
      }

      withSourcesJar()
      withJavadocJar()
    }

    dependencies {
      springBom platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")

      constraints {
        implementation "org.apache.poi:poi:$apachePoiVersion"
        implementation "org.apache.poi:poi-ooxml:$apachePoiVersion"
        implementation "org.modelmapper:modelmapper:$modelMapperVersion"
      }
    }

    test {
      useJUnitPlatform()
      jvmArgs = ["-XX:TieredStopAtLevel=1"]
    }

    tasks.withType(JavaCompile) {
      options.encoding = "UTF-8"
      options.compilerArgs << "-parameters"
    }

    parent.dependencies {
      jacocoAggregation subproject
    }
  }

  group = "net.croz.nrich"

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from isPlatformProject ? components.javaPlatform : components.java

        suppressAllPomMetadataWarnings()

        pom {
          afterEvaluate {
            name = project.name
            description = project.description
          }

          url = projectRepositoryUrl

          licenses {
            license {
              name = licenseName
              url = licenseUrl
            }
          }

          developers {
            developer {
              name = developerName
              email = developerEmail
              organization = developerOrganization
              organizationUrl = developerOrganizationUrl
            }
          }

          String gitRepositoryUrl = "scm:git:${projectRepositoryUrl}.git"
          scm {
            connection = gitRepositoryUrl
            developerConnection = gitRepositoryUrl
            url = projectRepositoryUrl
          }
        }
      }
    }
  }

  signing {
    // The gpg private key needs to be stored in ascii-armored format
    useInMemoryPgpKeys(System.getenv("GPG_PRIVATE_KEY"), System.getenv("GPG_PASSPHRASE"))
    sign publishing.publications.mavenJava
  }

  tasks.withType(Sign) {
    onlyIf {
      System.getenv("GPG_PRIVATE_KEY") && System.getenv("GPG_PASSPHRASE")
    }
  }
}

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl = uri("$sonatypeNexusUrl/service/local/")
      snapshotRepositoryUrl = uri("$sonatypeNexusUrl/content/repositories/snapshots/")

      username = sonatypeUsername
      password = System.getenv("SONATYPE_PASSWORD")

      // Specifying the configuration property explicitly rather than relying on the API call
      // is considered a performance optimization and can reduce execution time by several seconds
      stagingProfileId = System.getenv("SONATYPE_STAGING_PROFILE_ID")
    }
  }
}
